plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'org.jreleaser' version '0.4.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'com.github.ben-manes.versions' version '0.38.0'
}

group = 'com.redislabs'
description = 'Kafka Connect source and sink connectors for Redis Enterprise'

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/redis-developer/registry")
        credentials {
            username = project.findProperty("github.username") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    implementation 'org.apache.kafka:connect-api:2.8.0'
    implementation 'com.github.jcustenborder.kafka.connect:connect-utils:0.4.164'
    implementation 'com.redislabs:spring-batch-redis:2.13.0'
    testImplementation 'com.redislabs:testcontainers-redis:1.1.6'
    testImplementation 'org.testcontainers:junit-jupiter:1.15.3-REDISLABS'
    testImplementation 'io.projectreactor:reactor-test:3.4.6'
    testImplementation 'org.mockito:mockito-core:2.21.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.2'
    testImplementation 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
    testImplementation 'com.github.jcustenborder.kafka.connect:connect-utils-testing:0.4.164'
}

test {
    useJUnitPlatform()
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Redis Enterprise Kafka Connector'
                description = 'Kafka Connect source and sink connectors for Redis Enterprise'
                url = 'https://github.com/RedisLabs-Field-Engineering/redis-enterprise-kafka'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jruaux'
                        name = 'Julien Ruaux'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/RedisLabs-Field-Engineering/redis-enterprise-kafka'
                    developerConnection = 'scm:git:git@github.com:RedisLabs-Field-Engineering/redis-enterprise-kafka'
                    url = 'https://github.com/RedisLabs-Field-Engineering/redis-enterprise-kafka'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

def isNonStable = { String version ->
    def nonStableKeyword = ['PREVIEW'].any { it -> version.toUpperCase().contains(it) }
    def stableKeyword = ['RELEASE', 'FINAL', 'GA', 'JRE8'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+([.-]r)?$/
    return nonStableKeyword || (!stableKeyword && !(version ==~ regex))
}

tasks.named("dependencyUpdates").configure {

    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }

}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport


nexusPublishing {
    repositories {
        sonatype()
    }
}


jreleaser {
    project {
        name = 'redis-enterprise-kafka'
        description = 'Redis Enterprise Kafka Connector'
        longDescription = 'Kafka Connect source and sink connectors for Redis Enterprise'
        website = 'https://github.com/RedisLabs-Field-Engineering/redis-enterprise-kafka'
        authors = ['Julien Ruaux']
        tags = ['redis', 'enterprise', 'kafka']
        license = 'Apache-2.0'
    }
    release {
        github {
            username = 'jruaux'
            owner = 'RedisLabs-Field-Engineering'
            overwrite = true
        }
    }
}
